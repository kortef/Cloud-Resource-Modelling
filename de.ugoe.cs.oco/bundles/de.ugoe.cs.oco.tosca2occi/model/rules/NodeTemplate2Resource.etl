import "./../operations/generalOperations.etl";

//-----------------------------------------------//
//--------NodeTemplate2Resource------------------//
//-----------------------------------------------//
@greedy
rule NodeTemplate2Resource
	transform nodeTemplate : TOSCA!TNodeTemplate
	to resource : OCCI!Resource{ 
	guard: (not (configuration.get(nodeTemplate.type.localPart) == null))
	
	resource.id = nodeTemplate.id;	
	resource.title = nodeTemplate.name;
	resource.summary = nodeTemplate.getDescription();
	resource.assignKind(nodeTemplate);
	resource.setLinks(nodeTemplate);
	resource.setAttributeStates(nodeTemplate);
	//resource.setMixins(nodeTemplate);
	
	OCCI!Configuration.allInstances.first.resources.add(resource);
}

@greedy
rule NodeTemplate2ComponentMixin
	transform nodeTemplate : TOSCA!TNodeTemplate
	to mixinBase : OCCI!MixinBase{ 
	guard: ((mixinMapping.get(nodeTemplate.type.localPart) <> null) and 
		(OCCIExtensions!Mixin.allInstances.selectOne(m | m.name = mixinMapping.get(nodeTemplate.type.localPart)) <> null))
	var mixin = OCCIExtensions!Mixin.allInstances.selectOne(m | m.name = mixinMapping.get(nodeTemplate.type.localPart));
	if (mixin.isDefined()){
		mixinBase.setMixin(mixin);
		var resource = OCCI!Resource.allInstances.selectOne(r | r.id =  nodeTemplate.id);
		resource.getParts().add(mixinBase);
	}	
}

rule OperatingSystem2ImageMixin
	transform operatingSystem : TOSCA!OperatingSystem
	to mixinBase: OCCI!MixinBase{
	mixinBase.setMixin(mixin_Set.selectOne(m | m.name == "Ubuntu_XenialXerus"));
	for (requirement in operatingSystem.requirements.requirement){
		var r = TOSCA!TRelationshipTemplate.allInstances.selectOne(r | r.sourceElement.ref == requirement.id);
		var targetId = r.targetElement.ref;
		var c = TOSCA!TCapability.allInstances.selectOne(c | c.id == targetId);
		var resource = OCCI!Resource.allInstances.selectOne(r | r.id = c.econtainer.econtainer.id);
		resource.getParts().add(mixinBase);	
	}
}

@greedy
rule Server2FlavorMixin
	transform props : TOSCA!TServerProperties
	to mixinBase: OCCI!MixinBase{
	var mixin = null;
	if (props.numCpus.asInteger() == 1 and props.memory.asInteger() == 1024){
		mixin = OCCIExtensions!Mixin.allInstances.selectOne(m | m.name == "SWE_Small");
	} else if (props.numCpus.asInteger() == 2 and props.memory.asInteger() == 2048){
		mixin = mixin_Set.selectOne(m | m.name == "SWE_Medium");
	} else if (props.numCpus.asInteger() == 4 and props.memory.asInteger() == 4096){
		mixin = mixin_Set.selectOne(m | m.name == "SWE_Large");
	} else if (props.numCpus.asInteger() == 1 and props.memory.asInteger() == 4096){
		mixin = mixin_Set.selectOne(m | m.name == "SWE_Mem_Small");
	} else if (props.numCpus.asInteger() == 2 and props.memory.asInteger() == 8192){
		mixin = mixin_Set.selectOne(m | m.name == "SWE_Mem_Medium");
	} else if (props.numCpus.asInteger() == 4 and props.memory.asInteger() == 16384){
		mixin = mixin_Set.selectOne(m | m.name == "SWE_Mem_Large");
	} 
	mixinBase.setMixin(mixin);
	var resource = OCCI!Resource.allInstances.selectOne(r | r.id =  props.econtainer.econtainer.id);
	resource.getParts().add(mixinBase);
}
